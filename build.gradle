group 'se'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        gradleWrapperVersion = '4.10.2'
        springBootVersion  = '2.1.0.RELEASE'
        springCloudVersion = 'Finchley.RELEASE'
        dependencyManagementPluginVersion = '1.0.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    testStubbed {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testStubbed/java')
        }
        resources.srcDir file('src/testStubbed/resources')
    }
    testIntegration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/testIntegration/java')
        }
        resources.srcDir file('src/testIntegration/resources')
    }
}

configurations {
    testStubbedCompile.extendsFrom testCompile
    testStubbedRuntime.extendsFrom testRuntime
    testIntegrationCompile.extendsFrom testCompile
    testIntegrationRuntime.extendsFrom testRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web"
    compile "org.projectlombok:lombok:1.16.10"
    compile "org.aspectj:aspectjweaver:1.9.2"

    testCompile "org.springframework.boot:spring-boot-starter-test"

    testIntegrationCompile "info.cukes:cucumber-java:1.2.5"
    testIntegrationCompile "info.cukes:cucumber-junit:1.2.5"
    testIntegrationCompile "info.cukes:cucumber-spring:1.2.5"
}


task wrapper(type: Wrapper) {
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleWrapperVersion}-all.zip"
}


task testStubbed(type: Test) {
    include 'com/nab/se/helloWorld/test/stubbed/*.class'
    include 'com/nab/se/helloWorld/test/stubbed/*/*.class'
    systemProperties['spring.profiles.active'] = "testStubbed"
    testClassesDir = sourceSets.testStubbed.output.classesDir
    classpath = sourceSets.testStubbed.runtimeClasspath
    outputs.upToDateWhen { false }
}

task testIntegration(type: Test) {
    include 'com/nab/se/helloWorld/test/integration/configuration/testRunner/CucumberTestRunner.class'
    systemProperties['cucumber.options'] = "-t @IntegrationTest"
    systemProperties['spring.profiles.active'] = "testIntegration"
    testClassesDir = sourceSets.testIntegration.output.classesDir
    classpath = sourceSets.testIntegration.runtimeClasspath
    outputs.upToDateWhen { false }
}
